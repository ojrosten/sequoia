FUNCTION(sequoia_compile_options)
    if (MSVC)
        add_definitions(/W4)
        add_definitions(/bigobj)
        add_definitions(/MP)
    else()
        add_compile_options(-Wall -Wextra -Wpedantic)
        if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
            add_compile_options(-Wno-missing-braces -Wno-unused-lambda-capture -Wno-braced-scalar-init -Wno-unused-function)
        elseif(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
            add_compile_options(-Wno-comment)
        endif()
    endif()
ENDFUNCTION()

FUNCTION(sequoia_init)
    sequoia_compile_options()
	if(NOT MSVC)
		find_package(Threads REQUIRED)
        find_package(TBB REQUIRED)
	endif()
ENDFUNCTION()

FUNCTION(sequoia_link_libraries target)
    if(MSVC)
        target_link_libraries(${target} PUBLIC winmm)
    else()
        target_link_libraries(${target} PUBLIC Threads::Threads)
        target_link_libraries(${target} PUBLIC TBB::tbb)
    endif()
ENDFUNCTION()

FUNCTION(set_headers_for_ide target directory)
	file(GLOB_RECURSE HeaderFiles ${directory}/*.h*)
	target_sources(${target} PRIVATE ${HeaderFiles})
ENDFUNCTION()

FUNCTION(sequoia_compile_features target)
	target_compile_features(${target} PUBLIC cxx_std_23)
ENDFUNCTION()

FUNCTION(sequoia_finalize target headerDirForIDE) 
	sequoia_compile_features(${target})

    add_subdirectory(${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../Source TestFramework)
    target_link_libraries(${target} PUBLIC TestFramework)
	
	if (MSVC)    
        set_target_properties(${target} PROPERTIES LINK_FLAGS "/INCREMENTAL:NO")
		set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${target})
	endif()
	
	set_headers_for_ide(${target} ${headerDirForIDE})
ENDFUNCTION()

FUNCTION(sequoia_finalize_self target headersForIDE)
    target_include_directories(${target} PRIVATE ${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../TestCommon)
    target_include_directories(${target} PRIVATE ${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../Tests)

    sequoia_finalize(${target} ${headersForIDE})
ENDFUNCTION()

FUNCTION(sequoia_finalize_library target)
	sequoia_compile_features(${target})
    sequoia_link_libraries(${target})

	set_headers_for_ide(${target} ${CMAKE_CURRENT_LIST_DIR})
ENDFUNCTION()

FUNCTION(sequoia_finalize_executable target)
    sequoia_finalize_library(${target})

    if (MSVC)
        set_property(DIRECTORY ${CMAKE_CURRENT_LIST_DIR} PROPERTY VS_STARTUP_PROJECT ${target})
    endif()
endfunction()